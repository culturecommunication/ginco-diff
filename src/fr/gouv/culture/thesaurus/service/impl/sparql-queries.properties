# This software is governed by the CeCILL-B license under French law and
# abiding by the rules of distribution of free software. You can use,
# modify and/or redistribute the software under the terms of the CeCILL-B
# license as circulated by CEA, CNRS and INRIA at the following URL
# "http://www.cecill.info".
#
# As a counterpart to the access to the source code and rights to copy,
# modify and redistribute granted by the license, users are provided only
# with a limited warranty and the software's author, the holder of the
# economic rights, and the successive licensors have only limited
# liability.
#
# In this respect, the user's attention is drawn to the risks associated
# with loading, using, modifying and/or developing or reproducing the
# software by the user in light of its specific status of free software,
# that may mean that it is complicated to manipulate, and that also
# therefore means that it is reserved for developers and experienced
# professionals having in-depth computer knowledge. Users are therefore
# encouraged to load and test the software's suitability as regards their
# requirements in conditions enabling the security of their systems and/or
# data to be ensured and, more generally, to use and operate it in the
# same conditions as regards security.
#
# The fact that you are presently reading this means that you have had
# knowledge of the CeCILL-B license and that you accept its terms.

# -------------------------------------------------------------------------------------------------
# Gets the RDF type(s) of a resource.
# @param  uri   the resource URI
getRdfClasses = \
    SELECT ?type \
    WHERE { \
        ?uri rdf:type ?type . \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptSchemes present in repository.
# Each concept scheme is only described by a title, a last updated date and a submission date of the resource.
#
listConceptSchemes = \
	 CONSTRUCT { \
		?conceptScheme dc:title ?label . \
		?conceptScheme dct:modified ?lastUpdate . \
		?conceptScheme dct:dateSubmitted ?lastImport . \
	} WHERE { \
		?conceptScheme dc:title ?label . \
		OPTIONAL { \
			?conceptScheme dct:modified ?lastUpdate . \
		} \
		OPTIONAL { \
			?conceptScheme dct:dateSubmitted ?lastImport . \
		} \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptSchemes from a producer present in repository.
# Each concept scheme is only described by a title and a last updated date.
#
listConceptSchemesByProducer = \
	 CONSTRUCT { \
		?conceptScheme dc:title ?label . \
		?conceptScheme dct:modified ?lastUpdate . \
	} WHERE { \
		?conceptScheme dc:title ?label . \
		?conceptScheme dc:creator ?organisation . \
		?organisation foaf:name ?organisationName . \
		OPTIONAL { \
			?conceptScheme dct:modified ?lastUpdate . \
		} \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptSchemes related to a subject present in repository.
# Each concept scheme is only described by a title and a last updated date.
#
listConceptSchemesBySubject = \
	 CONSTRUCT { \
		?conceptScheme dc:title ?label . \
		?conceptScheme dct:modified ?lastUpdate . \
	} WHERE { \
		?conceptScheme dc:title ?label . \
		?conceptScheme dc:subject ?subject . \
		OPTIONAL { \
			?conceptScheme dct:modified ?lastUpdate . \
		} \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptSchemes present in repository.
# Each concept scheme is only described by a title and a last updated date.
#
listConceptSchemesProducers = \
	SELECT DISTINCT ?organisationName \
	WHERE { \
		?conceptScheme rdf:type skos:ConceptScheme . \
		?conceptScheme dc:creator ?organisation . \
        ?organisation foaf:name ?organisationName . \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptSchemes present in repository.
# Each concept scheme is only described by a title and a last updated date.
#
listConceptSchemesSubjects = \
	SELECT DISTINCT ?subject \
	WHERE { \
		?conceptScheme rdf:type skos:ConceptScheme . \
		?conceptScheme dc:subject ?subject . \
	}
#
# -------------------------------------------------------------------------------------------------
# Loads ConceptScheme data.
#
# @param  uri   the ConceptScheme URI
#
loadConceptScheme = \
    CONSTRUCT { ?uri ?property ?value } \
    WHERE { \
        ?uri ?property ?value . \
        FILTER (isLiteral(?value)) \
        }
#
# -------------------------------------------------------------------------------------------------
# Lists the top SKOS Concepts associated to a given ConceptScheme.
#
# @param  uri   the ConceptScheme URI
#
# Remarks:
#   Top concepts from the specified scheme are described. However, only triplets with a literal value
#   are returned.
#
listTopConcepts = \
    CONSTRUCT { ?concept ?property ?value . } \
    WHERE { \
        ?uri skos:hasTopConcept ?concept . \
        ?concept ?property ?value . \
        FILTER (isLiteral(?value)) \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptGroups associated to a given ConceptScheme.
#
# @param  uri   the ConceptScheme URI
#
# Remarks:
#   Concept groups from the specified scheme are described. However, only triplets with a literal value
#   are returned.
#
listConceptGroups = \
    CONSTRUCT { ?conceptGroup ?property ?value . } \
    WHERE { \
    	?conceptGroup skos:inScheme ?uri . \
    	?conceptGroup rdf:type ?type .\
    	?type rdfs:subClassOf iso-thes:ConceptGroup . \
    	?conceptGroup ?property ?value . \
    	FILTER (isLiteral(?value)) \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the SKOS ConceptGroups containing a given Concept.
#
# @param  uri   the Concept URI
#
# Remarks:
#   Concept groups from the specified concept are described. However, only triplets with a literal value
#   are returned.
#
listConceptGroupsFromConcept = \
    CONSTRUCT { ?conceptGroup ?property ?value . } \
    WHERE { \
    	?conceptGroup skos:member ?uri . \
    	?conceptGroup rdf:type ?type .\
    	?type rdfs:subClassOf iso-thes:ConceptGroup . \
    	?conceptGroup ?property ?value . \
    	FILTER (isLiteral(?value)) \
    }
#
# -------------------------------------------------------------------------------------------------
# Returns the graph associated to a given ConceptScheme.
# @param  uri   the ConceptScheme URI
constructConceptScheme = \
    CONSTRUCT { ?uri ?p ?o. ?s ?p ?uri } \
    WHERE { \
        { ?uri ?p ?o . } \
        UNION \
        { ?s ?p ?uri . } \
    } \
    ORDER BY (!BOUND(?o)) ?p ?o ?s
#
# -------------------------------------------------------------------------------------------------
# Loads all the Concept data.
#
# @param  uri   the Concept URI
#
# @returns
# 	triplets which subject is the concept to describe.
#
# Remarks:
#   the current query only considers literal objects, or specific associations.
loadConcept = \
    CONSTRUCT { ?uri ?property ?value } \
    WHERE { \
        ?uri ?property ?value . \
        FILTER (isLiteral(?value) \
        	|| sameTerm(?property, skos:broadMatch) \
        	|| sameTerm(?property, skos:closeMatch) \
        	|| sameTerm(?property, skos:exactMatch) \
        	|| sameTerm(?property, skos:narrowMatch) \
        	|| sameTerm(?property, skos:relatedMatch) \
        	|| sameTerm(?property, foaf:focus) \
        	|| sameTerm(?property, foaf:depiction) \
        	) \
        FILTER (?value != "") \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the Concept preferred Labels
# Retrieving more informations than before
#
# @param  uri   the Concept URI
#
# @returns
#	xl:prefLabel properties if it exists, skos:prefLabel literal value otherwise
#
# Remarks:
#   the current query allows retro-compatibility with skos:prefLabel
#	First part (before UNION) selects all xl:prefLabel and subProperties
#	Second part (after UNION) selects all skos:prefLabel except those which are sibling of xl:prefLabel  
loadConceptPrefLabels = \
    CONSTRUCT { \
    	?uri skos:prefLabel ?labelValue. \
    	?uri xl:prefLabel ?xlUri. \
    	?xlUri ?property ?value. \
	} \
    WHERE { \
		{ \
			?uri xl:prefLabel ?xlUri. \
			OPTIONAL { \
				?xlUri xl:literalForm ?labelValue. \
			} \
			OPTIONAL { \
				?xlUri ?property ?value. \
				FILTER (isLiteral(?value)) \
				FILTER (?value != "") \
			} \
		} \
		UNION \
		{ \
			?uri skos:prefLabel ?labelValue. \
			OPTIONAL { \
				?uri xl:prefLabel ?xlUri. \
			} \
			FILTER(!bound(?xlUri)) \
			FILTER (?labelValue != "") \
		} \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the Concept alternative Labels
#
# @param  uri   the Concept URI
#
# @returns
#	xl:prefLabel properties if it exists, skos:prefLabel literal value otherwise
#
# Remarks:
#   the current query allows retro-compatibility with skos:altLabel
#	First part (before UNION) selects all xl:altLabel and subProperties
#	Second part (after UNION) selects all skos:altLabel except those which are sibling of xl:altLabel  
loadConceptAltLabels = \
    CONSTRUCT { \
    	?uri skos:altLabel ?labelValue. \
    	?uri xl:altLabel ?xlUri. \
    	?xlUri ?property ?value. \
	} \
    WHERE { \
		{ \
			?uri xl:altLabel ?xlUri. \
			OPTIONAL { \
				?xlUri xl:literalForm ?labelValue. \
			} \
			OPTIONAL { \
				?xlUri ?property ?value. \
				FILTER (isLiteral(?value)) \
				FILTER (?value != "") \
			} \
		} \
		UNION \
		{ \
			?uri skos:altLabel ?labelValue. \
			OPTIONAL { \
				?uri xl:altLabel ?xlUri. \
			} \
			FILTER(!bound(?xlUri)) \
			FILTER (?labelValue != "") \
		} \
	}
#
# -------------------------------------------------------------------------------------------------
# Lists the concepts associated to a given Concept with a specific property.
#
# @param  uri    the Concept URI
# @param  link   the URI of the predicate bearing the relationship
#
# @returns
#   triplets where each subject is an RDF resource associated to the input concept
#
# Remarks:
#   the current query only considers the resource's prefLabel (if any).
#   all matching resources are assumed to be SKOS concepts.
listRelatedSkosConcepts = \
    CONSTRUCT { ?resource a skos:Concept ; skos:prefLabel ?label . } \
    WHERE { \
        ?uri ?link ?resource . \
        OPTIONAL { ?resource skos:prefLabel ?label . } \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the concepts associated to a given Concept with a specific property.
#
# @param  uri    the Concept URI
# @param  link   the URI of the predicate bearing the relationship
#
# @returns
#   triplets where each subject is an RDF resource associated to the input concept
#
# Remarks:
#   the current query only considers the resource's prefLabel (if any).
#   all matching resources are assumed to be SKOS concepts.
listParentSkosConcepts = \
    CONSTRUCT { ?resource a skos:Concept;  skos:prefLabel ?label; skos:broader ?broaderConcepts . } \
    WHERE { \
        ?uri skos:broader+ ?resource . \
        OPTIONAL { ?resource skos:prefLabel ?label . } \
    }
#
# -------------------------------------------------------------------------------------------------
# Décrit les collections des concepts associés à un concept d'origine.
#
# @param  uri    URI du concept d'origine
# @param  link   URI du prédicat reliant le concept d'origine aux concepts cibles
#
# @returns
#   triplets où chaque sujet corresond à une collection (ordonnée ou non) à laquelle
#   appartient un concept lié au concept d'origne via la relation spécifiée
#
# Remarks:
#   Dans cette requête, les collections ordonnées sont transformées en collections simples pour des 
#   besoins de simplification.
#
describeCollectionsFromRelatedSkosConcepts= \
    CONSTRUCT { ?collection a skos:Collection ; skos:prefLabel ?label ; skos:member ?resource . } \
    WHERE { \
        ?uri ?link ?resource . \
        { \
        	?collection a ?type . \
        	FILTER(sameTerm(?type, skos:Collection)) \
        	?collection skos:member ?resource . \
        } \
        UNION \
        { \
        	?collection a ?type . \
        	FILTER(sameTerm(?type, skos:OrderedCollection)) \
        	?collection skos:memberList/rdf:first+ ?resource . \
        	FILTER(!sameTerm(?resource, rdf:nil)) \
        } \
        OPTIONAL { ?collection skos:prefLabel ?label . } \
    }
#
# -------------------------------------------------------------------------------------------------
# Lists the concept schemes associated to a given Concept.
#
# @param  uri    the Concept URI
#
# Remarks:
#   the current query only considers the scheme's title.
listSchemesFromConcept = \
    CONSTRUCT { ?scheme dc:title ?label . } \
    WHERE { \
        ?uri skos:inScheme ?scheme . \
        ?scheme dc:title ?label . \
    }

# -------------------------------------------------------------------------------------------------
# Retrieves the ancestors of the concept which are at the
#  top level of he concept scheme
#
# @param  uri    the Concept URI
getTopAncestors = \
	CONSTRUCT {?ancestor ?property ?value . } \
	WHERE { \
  		?uri skos:broader+  ?ancestor . \
        ?ancestor ?property ?value . \
        FILTER NOT EXISTS { ?ancestor skos:broader ?x } \
        FILTER (isLiteral(?value)) \
	}
 
#
# -------------------------------------------------------------------------------------------------
# Returns the graph associated to a given Concept.
# @param  uri   the Concept URI
constructConcept = \
    CONSTRUCT { ?uri ?p ?o. ?s ?p ?uri } \
    WHERE { \
        { ?uri ?p ?o . } \
        UNION \
        { ?s ?p ?uri . } \
    } \
    ORDER BY (!BOUND(?o)) ?p ?o ?s
#
# -------------------------------------------------------------------------------------------------
# Returns organization name and home page to a given ConceptScheme
# 
# @param uri   the ConceptScheme URI
#
getThesaurusOrganizationWithConceptScheme = \
    SELECT ?organisationName ?organisationHomepage ?organisationMbox \
    WHERE { \
         ?uri dc:creator ?organisation . \
         ?organisation foaf:name ?organisationName . \
         OPTIONAL { ?organisation foaf:homepage ?organisationHomepage . } \
         OPTIONAL { ?organisation foaf:mbox ?organisationMbox . } \
    }  
#
# -------------------------------------------------------------------------------------------------
# Returns organization name and home page to a given Concept 
# @param uri   the Concept URI
getThesaurusOrganizationWithConcept = \
    SELECT ?organisationName ?organisationHomepage ?organisationMbox \
    WHERE { \
       ?uri skos:inScheme ?conceptScheme . \
       ?conceptScheme  dc:creator ?organisation . \
       ?organisation foaf:name ?organisationName . \
       ?organisation foaf:homepage ?organisationHomepage . \
       OPTIONAL { ?organisation foaf:mbox ?organisationMbox . } \
    }
#
# -------------------------------------------------------------------------------------------------
# Returns the "En savoir plus" link to a given ConceptScheme 
# @param uri   the ConceptScheme URI
getThesaurusSeeMoreWithConceptScheme = \
    SELECT ?seeMoreUrl \
    WHERE { \
      ?uri dc:relation ?seeMoreUrl \
    } 
#
# -------------------------------------------------------------------------------------------------
# Returns the "En savoir plus" link to a given Concept
# @param uri   the Concept URI
getThesaurusSeeMoreWithConcept = \
    SELECT ?seeMoreUrl \
    WHERE { \
      ?uri skos:inScheme ?conceptScheme . \
      ?conceptScheme  dc:relation ?seeMoreUrl \
    } 
#
# -----------------------------------------------------------------------------
# SEARCH QUERIES
# -----------------------------------------------------------------------------
#
# Renvoie le nombre de concepts correspondant à la requête.
# @param query
# 			Chaîne de caractères à rechercher
# @format-param 1$s
# 			Graph pattern de recherche (cf. searchConceptPattern)
# @returns
# 	?totalConcepts
# 			Nombre total de concepts trouvés
searchConceptFetchMatchCount= \
	SELECT (COUNT(DISTINCT *) AS ?totalConcepts) \
	WHERE { \
	  %1$s \
	}
#
# Renvoie le nombre de concepts correspondant à la requête.
# @param query
# 			Chaîne de caractères à rechercher
# @format-param 1$s
# 			Graph pattern de recherche (cf. searchConceptPattern)
# @format-param 2$s
# 			Chaîne de caractères donnant la clause ORDER BY pour le tri
# @format-param 3$d
# 			Entier donnant l'index du premier résultat
# @format-param 4$d
# 			Entier donnant le nombre de résultats par page
# @returns
# 	?concept
# 			URI du concept trouvé
# 	?conceptPrefLabel
# 			Libellé préférentiel du concept trouvé
# 	?scheme
# 			URI du concept scheme du concept trouvé
# 	?schemeTitle
# 			Titre du concept scheme
# 	?label
# 			Libellé ayant matché la requête
searchConceptFetchResults= \
	SELECT DISTINCT ?concept ?conceptPrefLabel ?scheme ?schemeTitle ?label \
	WHERE { \
	  %1$s \
	} \
	%2$s \
	OFFSET %3$d LIMIT %4$d
